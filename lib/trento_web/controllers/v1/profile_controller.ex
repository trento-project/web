defmodule TrentoWeb.V1.ProfileController do
  use TrentoWeb, :controller
  use OpenApiSpex.ControllerSpecs

  import Trento.Infrastructure.SSO.SSO, only: [sso_enabled?: 0]

  alias OpenApiSpex.Operation

  alias Trento.Users
  alias Trento.Users.User
  alias TrentoWeb.OpenApi.V1.Schema

  plug TrentoWeb.Plugs.ExternalIdpGuardPlug
       when action in [:reset_totp, :get_totp_enrollment_data, :confirm_totp_enrollment]

  plug OpenApiSpex.Plug.CastAndValidate, json_render_error_v2: true

  plug TrentoWeb.Plugs.LoadUserPlug
  action_fallback TrentoWeb.FallbackController

  operation :show,
    summary: "Get profile.",
    tags: ["Profile", "MCP"],
    description:
      "Returns the profile information of the currently authenticated user, supporting account management and personalization features.",
    responses: [
      ok:
        {"Profile information of the currently authenticated user, supporting account management and personalization features.",
         "application/json", Schema.User.UserProfile}
    ]

  def show(conn, _) do
    %User{} = user = Pow.Plug.current_user(conn)
    render(conn, :profile, user: user)
  end

  # open_api_operation is used instead of the operation macro as sso_enabled()? function
  # uses a runtime value which cannot be obtained during compile time with the macro
  def open_api_operation(:update) do
    %Operation{
      summary: "Update the current user profile.",
      description:
        "Updates the profile of the currently authenticated user with new information, supporting account management and personalization features.",
      tags: ["Profile"],
      operationId: "TrentoWeb.V1.ProfileController.update",
      requestBody:
        if sso_enabled?() do
          Operation.request_body(
            "Request containing updated profile information for the currently authenticated user. As SSO is enabled only certain fields can be updated.",
            "application/json",
            Schema.User.UserProfileUpdateSSOEnabledRequest
          )
        else
          Operation.request_body(
            "Request containing updated profile information for the currently authenticated user.",
            "application/json",
            Schema.User.UserProfileUpdateRequest
          )
        end,
      responses: %{
        200 =>
          Operation.response(
            "Profile update was successful, returning the updated user profile information for account management.",
            "application/json",
            Schema.User.UserProfile
          ),
        422 => Schema.UnprocessableEntity.response(),
        403 => Schema.Forbidden.response()
      }
    }
  end

  def update(conn, _) do
    %User{} = user = Pow.Plug.current_user(conn)
    profile_params = OpenApiSpex.body_params(conn)

    with {:ok, %User{} = updated_user} <-
           update_user_profile(sso_enabled?(), user, profile_params) do
      render(conn, :profile, user: updated_user)
    end
  end

  operation :reset_totp,
    summary: "Reset the TOTP configuration for the user.",
    description:
      "Resets the Time-based One-Time Password (TOTP) configuration for the currently authenticated user, allowing them to re-enroll in two-factor authentication if needed.",
    tags: ["Profile"],
    responses: [
      forbidden: Schema.Forbidden.response(),
      no_content: "User TOTP enrollment reset."
    ]

  def reset_totp(conn, _) do
    %User{} = user = Pow.Plug.current_user(conn)

    with {:ok, %User{}} <- Users.reset_totp(user) do
      send_resp(conn, :no_content, "")
    end
  end

  operation :get_totp_enrollment_data,
    summary: "Get TOTP enrollment data.",
    description:
      "Returns TOTP enrollment data, including a QR code and related information, to help the user set up two-factor authentication for their account.",
    tags: ["Profile"],
    responses: [
      ok:
        {"TOTP enrollment data including QR code and related information for two-factor authentication setup.",
         "application/json", Schema.User.UserTOTPEnrollmentPayload},
      unprocessable_entity: Schema.UnprocessableEntity.response(),
      forbidden: Schema.Forbidden.response()
    ]

  def get_totp_enrollment_data(conn, _) do
    %User{} = user = Pow.Plug.current_user(conn)

    with {:ok, enrollment_payload} <- Users.initiate_totp_enrollment(user) do
      render(conn, :totp_enrollment_data, enrollment_payload: enrollment_payload)
    end
  end

  operation :confirm_totp_enrollment,
    summary: "Confirm TOTP enrollment procedure.",
    description:
      "Confirms the TOTP enrollment process by validating the code generated by the authenticator app, enabling two-factor authentication for the user account.",
    tags: ["Profile"],
    request_body:
      {"Request containing TOTP enrollment confirmation code for enabling two-factor authentication.",
       "application/json", Schema.User.UserTOTPEnrollmentConfirmRequest},
    responses: [
      ok:
        {"TOTP enrollment process completed successfully, enabling two-factor authentication for the user account.",
         "application/json", Schema.User.UserTOTPEnrollmentConfirmPayload},
      unprocessable_entity: Schema.UnprocessableEntity.response(),
      forbidden: Schema.Forbidden.response()
    ]

  def confirm_totp_enrollment(conn, _) do
    %User{} = user = Pow.Plug.current_user(conn)

    %{totp_code: totp_code} = OpenApiSpex.body_params(conn)

    with {:ok, %User{totp_enabled_at: totp_enabled_at}} <-
           Users.confirm_totp_enrollment(user, totp_code) do
      render(conn, :totp_enrollment_completed, %{totp_enabled_at: totp_enabled_at})
    end
  end

  defp update_user_profile(false, user, profile_params),
    do: Users.update_user_profile(user, profile_params)

  defp update_user_profile(true, user, profile_params),
    do: Users.update_user_profile_sso_enabled(user, profile_params)
end
